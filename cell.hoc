begintemplate B_BallStick
public is_art
public init, topol, basic_shape, subsets, geom, biophys, geom_nseg, biophys_inhomo
public synlist, x, y, z, position, connect2target

public soma, dend
public all, den

create soma, dend[1]

objref synlist

proc init() {local i
  nden = $o1.size()
  topol($o1)
  for i=0, nden-1 {dend[i].nseg = $o2.x[i] }
    
  subsets()
  geom()
  biophys()
  synlist = new List()
  synapses()
  x = y = z = 0 // only change via position
}


proc topol() { local i
  create dend[$o1.size]
  connect dend[0](0), soma(1)
  for i=1, $o1.size() - 1 {
    connect dend[i](0), dend[$o1.x[i]](1)
  }
  basic_shape()
}
proc basic_shape() {
  soma {pt3dclear() pt3dadd(0, 0, 0, 1) pt3dadd(15, 0, 0, 1)}
  dend[0] {pt3dclear() pt3dadd(15, 0, 0, 1) pt3dadd(105, 0, 0, 1)}
}

objref all, den
proc subsets() { local i
  objref all, den
  all = new SectionList()
    forall all.append()
  den = new SectionList()
    for i=0, nden-1 dend[i] { den.append() }

}
proc geom() { local i
  forsec all {  }
  soma {  /*area = 500 */ L = diam = 12.6157  }
  dend[0] {  L = 200  diam = 1  }
  for i=1, nden-1 dend[i] { L = 10 diam = 1 }
}
external lambda_f
proc biophys() {
  forsec all {
    Ra = 100
    cm = 1
  }
  forsec all {
    insert pas
      g_pas = 0.001
      e_pas = -65
    insert hh2
//    insert hh3
  }
}
proc biophys_inhomo(){}
proc position() { local i
  soma for i = 0, n3d()-1 {
    pt3dchange(i, $1-x+x3d(i), $2-y+y3d(i), $3-z+z3d(i), diam3d(i))
  }
  x = $1  y = $2  z = $3
}
obfunc connect2target() { localobj nc //$o1 target point process, optional $o2 returned NetCon
  soma nc = new NetCon(&v(1), $o1)
  nc.threshold = 10
  if (numarg() == 2) { $o2 = nc } // for backward compatibility
  return nc
}
objref syn_
proc synapses() {
  /* E0 */   dend[0] syn_ = new ExpSyn(0.8)  synlist.append(syn_)
    syn_.tau = 2
  /* I1 */   dend[0] syn_ = new ExpSyn(0.1)  synlist.append(syn_)
    syn_.tau = 5
    syn_.e = -80
}
func is_art() { return 0 }

endtemplate B_BallStick
